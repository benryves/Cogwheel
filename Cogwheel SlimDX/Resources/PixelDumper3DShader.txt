// Parameters:

uniform extern texture LeftEye;  // Left eye texture.
uniform extern texture RightEye; // Right eye texture.

uniform extern float4 LeftEyeColour;  // Left eye colour when using anaglyphs.
uniform extern float4 RightEyeColour; // Right eye colour when using anaglyphs.

uniform extern float ViewportWidth;  // Width of the viewport in pixels.
uniform extern float ViewportHeight; // Height of the viewport in pixels.

// Types:

struct VertexPositionTextureTexture {
    float4 Position : POSITION0;
	float2 Texture : TEXCOORD0;
	float2 NormalisedTexture : TEXCOORD1;
};

// Texture samplers:

sampler LeftEyeSampler = sampler_state {
	texture = <LeftEye>;
	addressu = CLAMP;
	addressv = CLAMP;
};

sampler RightEyeSampler = sampler_state {
	texture = <RightEye>;
	addressu = CLAMP;
	addressv = CLAMP;
};

// Vertex shaders:

VertexPositionTextureTexture VertexShaderFunction(VertexPositionTextureTexture input) {
    return input;
}

// Pixel shaders:

float4 LeftEyeOnlyPixelShader(VertexPositionTextureTexture input) : COLOR0 {
	return tex2D(LeftEyeSampler, input.Texture);
}

float4 RightEyeOnlyPixelShader(VertexPositionTextureTexture input) : COLOR0 {
	return tex2D(RightEyeSampler, input.Texture);
}

float4 RowInterleavedPixelShader(VertexPositionTextureTexture input) : COLOR0 {
	float row = input.NormalisedTexture.y * ViewportHeight * 0.5f;
	if (abs(round(row) - row) < 0.1f) {
		return tex2D(LeftEyeSampler, input.Texture);
	} else {
		return tex2D(RightEyeSampler, input.Texture);
	}
}

float4 ColumnInterleavedPixelShader(VertexPositionTextureTexture input) : COLOR0 {
	float col = input.NormalisedTexture.x * ViewportWidth * 0.5f;
	if (abs(round(col) - col) < 0.1f) {
		return tex2D(LeftEyeSampler, input.Texture);
	} else {
		return tex2D(RightEyeSampler, input.Texture);
	}
}

float4 ChequerboardInterleavedPixelShader(VertexPositionTextureTexture input) : COLOR0 {
	float pixel = (input.NormalisedTexture.x * ViewportWidth + input.NormalisedTexture.y * ViewportHeight) * 0.5f;
	if (abs(round(pixel) - pixel) < 0.1f) {
		return tex2D(LeftEyeSampler, input.Texture);
	} else {
		return tex2D(RightEyeSampler, input.Texture);
	}
}

float4 ColourAnaglyphPixelShader(VertexPositionTextureTexture input) : COLOR0 {
	return tex2D(LeftEyeSampler, input.Texture) * LeftEyeColour + tex2D(RightEyeSampler, input.Texture) * RightEyeColour;
}

float4 MonochromeAnaglyphPixelShader(VertexPositionTextureTexture input) : COLOR0 {
	float4 rgbToLuma = float4(0.2109375f, 0.71484375f, 0.07421875f, 0.0f);
	return dot(rgbToLuma, tex2D(LeftEyeSampler, input.Texture)) * LeftEyeColour + dot(rgbToLuma, tex2D(RightEyeSampler, input.Texture)) * RightEyeColour;
}

// Techniques:

technique LeftEyeOnly {
    pass Pass1 {
        VertexShader = compile vs_1_1 VertexShaderFunction();
        PixelShader = compile ps_2_0 LeftEyeOnlyPixelShader();
    }
}

technique RightEyeOnly {
    pass Pass1 {
        VertexShader = compile vs_1_1 VertexShaderFunction();
        PixelShader = compile ps_2_0 RightEyeOnlyPixelShader();
    }
}

technique RowInterleaved {
    pass Pass1 {
        VertexShader = compile vs_1_1 VertexShaderFunction();
        PixelShader = compile ps_2_0 RowInterleavedPixelShader();
    }
}

technique ColumnInterleaved {
    pass Pass1 {
        VertexShader = compile vs_1_1 VertexShaderFunction();
        PixelShader = compile ps_2_0 ColumnInterleavedPixelShader();
    }
}

technique ChequerboardInterleaved {
    pass Pass1 {
        VertexShader = compile vs_1_1 VertexShaderFunction();
        PixelShader = compile ps_2_0 ChequerboardInterleavedPixelShader();
    }
}

technique ColourAnaglyph {
    pass Pass1 {
        VertexShader = compile vs_1_1 VertexShaderFunction();
        PixelShader = compile ps_2_0 ColourAnaglyphPixelShader();
    }
}

technique MonochromeAnaglyph {
    pass Pass1 {
        VertexShader = compile vs_1_1 VertexShaderFunction();
        PixelShader = compile ps_2_0 MonochromeAnaglyphPixelShader();
    }
}